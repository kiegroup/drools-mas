/*
 * Copyright 2011 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
  Content Based routing general rules
  When a message arrives, CBR rules determine its destination (the session(s) where it needs to be processed),
  while processing rules determine the commands to be executed to deliver the contents to that session.
  When the two steps have been completed, the commands are delivered.
*/

package org.drools.mas;
import org.drools.mas.core.SessionManager;
import org.drools.runtime.StatefulKnowledgeSession;
import java.util.Map;
import org.drools.mas.util.LoggerHelper;
import org.drools.mas.core.DroolsAgentConfiguration;
import org.drools.mas.util.helper.SessionLocator;
import org.drools.grid.helper.GridHelper;

query mapSession( String $key, String $sid )
    SessionMapping( $key, $sid; )
end

rule "log"
    when
       $s: SessionMapping()
    then
        LoggerHelper.debug("  !#!@#!@#!@#!@####  Session Mapping: " + $s);
end


rule "generateSession"
salience 9999
when
    $qry : DroolsQuery(  name == "mapSession", $el : elements[0] )
    $key : String( this != "" ) from $el
    not SessionMapping( $key ;  )
    $conf: DroolsAgentConfiguration()
then
    String sid = $key;
    String targetLocation = $conf.getMindNodeLocation(); //apply load balancing policy here
    if(LoggerHelper.isDebugEnabled()){
        LoggerHelper.debug("  !#!@#!@#!@#!@#### Generating Session: " + sid + " with CS conf: "+ $conf.getDefaultSubsessionChangeSet() );
    }
    
    StatefulKnowledgeSession kSession =
        SessionManager.create( sid,
                               $conf,
                               new DroolsAgentConfiguration.SubSessionDescriptor( sid,
                                                                                  null, // use default changeset
                                                                                  targetLocation ),
                               GridHelper.getGrid() ).getStatefulKnowledgeSession();
    kSession.insert(new SessionLocator( targetLocation, sid ) );                           
    kSession.fireAllRules();
        
    //local for now.. 
    insert( new SessionLocator( targetLocation, sid ) );
    insert( new SessionMapping( $key, sid ) );
end





